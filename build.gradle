['java', 'maven', 'signing', 'maven-publish'].each {
    apply plugin : it
}

group = 'com.fdflib'
version = '1.4.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12',
            'mysql:mysql-connector-java:5.1.44',
            'org.mariadb.jdbc:mariadb-java-client:2.7.0',
            'org.postgresql:postgresql:9.4-1200-jdbc41',
            'org.hsqldb:hsqldb:2.3.4',
            'com.zaxxer:HikariCP:2.5.1'
    testCompile group: 'junit', name: 'junit', version: '4.8.2'
}

task sourcesJar (type : Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar (type: Jar, dependsOn: javadoc) { // (1)
    classifier = 'javadoc'
    from javadoc.destinationDir
}


publishing {
    publications {
        fdflib(MavenPublication) {
            artifactId '4dflib'

            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username ossrhUsername
                password ossrhPassword
            }

            if(project.version.endsWith('-SNAPSHOT')) {
                url 'https://oss.sonatype.org/content/repositories/snapshots'
            } else {
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            }
        }
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name '4dflib'
                packaging 'jar'
                description '4DF (4th Dimensional Form) Library and ORM Tool'
                url 'https://github.com/briangormanly/4dflib'
                scm {
                    connection 'scm:git:https://github.com/briangormanly/4dflib.git'
                    developerConnection 'scm:git:git@github.com/briangormanly/4dflib.git'
                    url 'https://github.com/briangormanly/4dflib.git'
                }
                licenses {
                    license {
                        name 'GNU Lesser General Public License v3 (LGPL)'
                        url 'http://www.gnu.org/copyleft/lesser.html'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'bgormanly'
                        name 'Brian Gormanly'
                        email 'bgormanly@gmail.com'
                    }
                }
            }
        }
    }
}
